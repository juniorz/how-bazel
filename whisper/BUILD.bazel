load("@io_bazel_rules_go//go:def.bzl", "go_binary", "go_test")
load("@rules_pkg//:pkg.bzl", "pkg_tar")
load("@aspect_bazel_lib//lib:transitions.bzl", "platform_transition_filegroup")
load("@rules_oci//oci:defs.bzl", "oci_image", "oci_image_index", "oci_tarball", "oci_push")

go_binary(
    name = "whisper",
    srcs = [
        "main.go",
        "greeter.go",
    ],
)

go_test(
    name = "whisper_test",
    srcs = [
        "greeter.go",
        "greeter_test.go",
    ],
)

pkg_tar(
    name = "app_layer",
    srcs = [":whisper"],
)

oci_image(
    name = "image",
    base = "@distroless_base",
    tars = [":app_layer"],
    entrypoint = ["/whisper", "--daemon", "any-other-argument"],
    target_compatible_with = select({
        "@platforms//cpu:x86_64": ["@platforms//os:linux"],
        "@platforms//cpu:arm64": ["@platforms//os:linux"],
        "//conditions:default": ["@platforms//:incompatible"], # not compatible otherwise
    }),
)

oci_tarball(
    name = "tarball",
    image = ":image",
    repo_tags = [
        "juniorz/whisper:dev",
    ],
)

platform_transition_filegroup(
    name = "image_arm64",
    srcs = [":image"],
    target_platform = "@io_bazel_rules_go//go/toolchain:linux_arm64",
)

platform_transition_filegroup(
    name = "image_amd64",
    srcs = [":image"],
    target_platform = "@io_bazel_rules_go//go/toolchain:linux_amd64",
)

oci_image_index(
    name = "multiarch_image",
    images = [
        ":image_arm64",
        ":image_amd64",
    ],
)

oci_push(
    name = "push_image",
    image = ":multiarch_image",
    repository = "index.docker.io/juniorz/whisper",
    remote_tags = ["latest"],
)
